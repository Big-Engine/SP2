#include "interfaces/Prop_Interface.hps"
#include "base/Player_Types.hps"
#include "custom/helpers/helper_items.hps"
#include "custom/components/ComponentInterfaces_Custom.hps"
#include "custom/helpers/helper_modules_custom.hps"

class cScrPropPickup : iScrProp, iScrProp_Interface
{
	//------------------------------------------------------------
	
	/////////////////////////////////////////
	// INIT
	/////////////////////////////////////////
	
	//------------------------------------------------------------
	
	void Init()
	{
	}
	
	//------------------------------------------------------------
	
	/////////////////////////////////////////
	// LOADING
	/////////////////////////////////////////
	
	//------------------------------------------------------------
	
	void SetupAfterLoad(cWorld @apWorld, cResourceVarsObject@ apVars, cResourceVarsObject@ apInstanceVars)
	{
		tString sItemType = apVars.GetVarString("ItemType", "");
		tString sOverrideItemType = apInstanceVars.GetVarString("ItemType","");
		if (sOverrideItemType!="")
			sItemType = sOverrideItemType;
			
		if (sItemType=="")
			Error("[Prop_Pickup] No item type specified in entity or editor for "+mBaseObj.GetName()+" ("+mBaseObj.GetFileName()+")");
			
		mBaseObj.SetVarString("Item_Type", sItemType);
		msType = sItemType;
			
		tString sItemContentSubType = apVars.GetVarString("ContentSubType", "");
		tString sOverrideSubType = apInstanceVars.GetVarString("ContentSubType","");
		if (sOverrideSubType!="")
			sItemContentSubType = sOverrideSubType;
			
		mlItemContentLevel = apVars.GetVarInt("ContentLevel",0);
		mlItemCustomContentLevel = apInstanceVars.GetVarInt("ContentLevel",-1);
		if (mlItemCustomContentLevel!=-1)
		{
			mlItemContentLevel = mlItemCustomContentLevel;
		}
		else
		{
			if (msType == "Matchbook")
			{
				mlItemContentLevel = cMath_RandRectl(1,2);
			}
		}
		
		mbHighlight = apInstanceVars.GetVarBool("Highlight", true);
		
		mBaseObj.SetVarString("Item_Content_SubType", sItemContentSubType);
		mBaseObj.SetVarInt("Item_Content_Level", mlItemContentLevel);
		
		mbOnlyNeedOne = apVars.GetVarBool("OnlyNeedOne",false);
		mbPutAwayAfter = apVars.GetVarBool("PutAwayAfter",true);
		
		mbUseFullnessAnimation = apVars.GetVarBool("UseFullnessAnimation",false);
		
		//////////////////////////
		// User Components
		if (apVars.GetVarBool("IsMeat", false))
		{
			@mpMeat = UserComponent_MeatComponent_Create(mBaseObj);
			mbIsMeat = true;
			
			//cLux_AddDebugMessage("Attract Distance = " + apVars.GetVarFloat("MeatAttractDistance", 5.0f));
			//cLux_AddDebugMessage("Max Attract Distance = " + apVars.GetVarFloat("MeatAttractDistanceMax", 5.0f));
			
			mpMeat.SetAttractDistance(apVars.GetVarFloat("MeatAttractDistance", 5.0f));
			mpMeat.SetMaxAttractDistance( apVars.GetVarFloat("MeatAttractDistanceMax", 10.0f));
		}
		
		msAnimationName = apVars.GetVarString("AnimationName", "");
		
		tString sPickupSound = apVars.GetVarString("PickupSound", "");
		msPickupSound = sPickupSound!="" ? sPickupSound : "player/UI/menu/add_inventory";
				
		mBaseObj.SetFullGameSave(true);
	}
	
	//------------------------------------------------------------
	
	/////////////////////////////////////////
	// GENERAL
	/////////////////////////////////////////
	
	//------------------------------------------------------------
	
	void OnUpdate(float afTimeStep)
	{
		if (mbUseFullnessAnimation)
			UpdateFullnessAnimation();
	}
	
	//------------------------------------------------------------
	
	void OnPostUpdate(float afTimeStep)
	{
	}
	
	//------------------------------------------------------------
	
	void ResetProperties()
	{
		 
	}
	
	//------------------------------------------------------------
	
	void BeforeEntityDestruction()
	{
	}
		
	//------------------------------------------------------------
	
	void OnStartMove()
	{
	}
	
	//------------------------------------------------------------
	
	void OnConnectionStateChange(iLuxEntity@ apEntity,int alState)
	{
	}
	
	void UpdateFullnessAnimation()
	{
		int numStates = mBaseObj.GetMeshEntity().GetAnimationStateNum();
		if (numStates <= 0) return;
		if (mbUseFullnessAnimation)
		{
			tString sItem = Item_GetHeld(); 
			float fFullness = Item_GetFullness(sItem); 
			int lIdx = 0; 
			
			mBaseObj.GetMeshEntity().GetAnimationState(lIdx).SetPaused(true); 
			mBaseObj.GetMeshEntity().GetAnimationState(lIdx).SetRelativeTimePosition(fFullness);
		}
	}
	
	//------------------------------------------------------------
	
	/////////////////////////////////////////
	// ACTIONS
	/////////////////////////////////////////
	
	//------------------------------------------------------------
	
	void OnSetActive(bool abX)
	{
	
	}
	
	//------------------------------------------------------------
	
	void GiveDamage(float afAmount, int alStrength, const tString&in asType, tID a_idSource)
	{
		
	}
	
	//------------------------------------------------------------
	
	void OnHealthChange()
	{
	}
	//------------------------------------------------------------
	
	//NOTE: This is only useable if SetScriptCollisionCallbackActive is true
	void OnPhysicsCollision(iPhysicsBody @apBody, iPhysicsBody @apCollideBody, cPhysicsContactData&in aContactData)
	{
		//Debug:
		//cLux_AddDebugMessage("Body "+apBody.GetName()+" vs "+apCollideBody.GetName() + "Nrm: " +aContactData.mfMaxContactNormalSpeed + " Tan: "+aContactData.mfMaxContactTangentSpeed, false);
	}
	
	//------------------------------------------------------------
	
	/////////////////////////////////////////
	// INTERACTION
	/////////////////////////////////////////
	
	//------------------------------------------------------------
		
	bool CanInteract(int alType, iPhysicsBody@ apBody)
	{
		mCursor = eCrossHairState_PickUp;
		if (mbOnlyNeedOne)
		{
			if (ItemType_IsInInventory(msType))
			{
				mCursor = eCrossHairState_PickUpForbidden;
				return true;
			}
		}
		if (!ItemType_WillFit(msType))
		{
			mCursor = eCrossHairState_PickUpForbidden;
		}
		return true;
	}
	
	//------------------------------------------------------------
		
	bool OnInteract(int alType, iPhysicsBody@ apBody, const cVector3f &in avFocusPos, const tString&in asData)
	{
		// global reaction for picking up oil after having lost the lantern
		if ((msType == "HerbertOil" || msType == "ModernOil" || msType == "AlchemistOil") && 
			cScript_GetGlobalVarBool("Global_LanternLost")) 
		{
			if (!Pregnancy_GetWhispering())
				Voice_PlayDelayed("Global_Lantern_TakeOilButLost", 0.5);
		}
		
		if (!ItemType_WillFit(msType))
			return false;
			
		if (mbOnlyNeedOne)
		{
			if (ItemType_IsInInventory(msType))
				return false;
		}
			
		if (mbIsMeat)
			mpMeat.ClearCurrentAttraction();
			
		if (msType == "Matchbook")
		{
			int lMaxInventoryContentLevel = 1000;
			int lInventoryContentLevel = Matches_GetCount();
			int lContentLevel;
			
			if (mlItemCustomContentLevel == -1)
			{
				if (lInventoryContentLevel <= 1)
				{
					float fChance = cMath_RandRectf(0,1);
					lContentLevel = (fChance > .6f) ? 3 : 2;
				}
				else
				{
					int lLuckyRoll = cScript_GetGlobalVarFloat("LuckyRoll");
					if (lLuckyRoll == 0) lLuckyRoll = 32;
					
					int lRoll = cMath_RandRectl(1,32);	//cLux_AddDebugMessage("Roll: "+lRoll+" - Chance: [1/"+lLuckyRoll+"]");
					if (lRoll > lLuckyRoll)	// Can't happen twice in a row
					{
						lContentLevel = 3;
						cScript_SetGlobalVarFloat("LuckyRoll",32);
					}
					else
					{
						float fChance = cMath_RandRectf(0,1);
						lContentLevel = (fChance > 0.2f) ? 2 : 1;
						cScript_SetGlobalVarFloat("LuckyRoll",lLuckyRoll-1);  //cLux_AddDebugMessage("Missed Lucky Roll. New chance: [1/"+lLuckyRoll+"]");
					}
				}
			}
			else
			{
				lContentLevel = mlItemContentLevel;
			}
				
			if (lInventoryContentLevel + mlItemContentLevel > lMaxInventoryContentLevel)
			{
				lContentLevel = lMaxInventoryContentLevel - lInventoryContentLevel;
			}

			mBaseObj.SetVarInt("Item_Content_Level", lContentLevel);
		}
		
		mBaseObj.PlaySound("PickupSound",msPickupSound,true, true);
		
		PickUp();
		
		return true;
	}
	
	//------------------------------------------------------------
	
	int GetInteractIconId(int alType, iPhysicsBody@ apBody)
	{
		return mCursor;
	}
	
	//------------------------------------------------------------
	
	/////////////////////////////////////////
	// GLOBAL
	/////////////////////////////////////////
	
	//------------------------------------------------------------
	
	void PickUp()
	{
		Item_PickUpEntity(mBaseObj.GetName(), false, mbPutAwayAfter);
	}
	
	//------------------------------------------------------------
	
	void _Global_GetCanBeHighlighted()
	{
		cScript_SetGlobalReturnBool(mbHighlight);
	}
	
	//------------------------------------------------------------
	
	void _Global_SetCanBeHighlighted()
	{
		mbHighlight = cScript_GetGlobalArgBool(0);
	}
	
	//------------------------------------------------------------
	
	void _Global_GetAnimationName()
	{
		tString sAnimName = msAnimationName;
		cScript_SetGlobalReturnString(sAnimName);
	}
	
	//------------------------------------------------------------
	
	void _Global_GetAnimationNameSpecific()
	{
		cScript_SetGlobalReturnString("");
	}
	
	//------------------------------------------------------------
	
	void _Global_SetAnimationName()
	{
		msAnimationName = cScript_GetGlobalArgString(0);
	}
	
	//------------------------------------------------------------
	
	void _Global_CopyFromItemEntity_Base()
	{
		tString sDest = cScript_GetGlobalArgString(0);

		cScript_SetGlobalArgString(0, msAnimationName);
		cScript_RunGlobalFunc(sDest, "cScrPropPickup", "_Global_SetAnimationName");
	}
	
	//------------------------------------------------------------
	
	void _Global_CopyFromItemEntity()
	{
		tString sDest = cScript_GetGlobalArgString(0);
		
		// Copy properties we need to preserve!
		// e.g. active effects etc.
		// See cScrPropLantern for how to copy explicit script properties across
	}
	
	//------------------------------------------------------------
	
	void _Global_OnItemTakenOut()
	{
		tString sItem = cScript_GetGlobalArgString(0);
	}

	//------------------------------------------------------------

	void _Global_OnItemPutAway()
	{
		tString sItem = cScript_GetGlobalArgString(0);
	}
	
	//------------------------------------------------------------
	
	void _Global_OnItemThrown()
	{
		tString sItemType = cScript_GetGlobalArgString(0);
		
		if (mbIsMeat)
			mpMeat.SetAttractEnabled(true);
	}
	
	//------------------------------------------------------------
	
	void _Global_SetUnreachable()
	{
		if (mbIsMeat)
			mpMeat.SetUnreachable();
	}
	
	//--------------------------------------------------------------
	// This is SOLELY for the sketchbook in the wreck level - I didn't want to
	// add a whole new prop class just for this.
	
	void _Global_SetUpGui()
	{
		tString sSubMesh = cScript_GetGlobalArgString(0);
		tString sGuiFunction = cScript_GetGlobalArgString(1);
		cVector2f vScreenSize = cScript_GetGlobalArgVector2f(2);
		SetUpGui(sSubMesh, sGuiFunction, vScreenSize);
	}
	
	//------------------------------------------------------------
	
	void SetUpGui(const tString& in asSubmesh, const tString& in asGuiFunction, const cVector2f avScreenSize)
	{
		mBaseObj.DisableSubMeshMerge(asSubmesh);
		mBaseObj.CreateAndSetupGui(asSubmesh, cColor(1,1,1,1), cColor(1,1,1,1), cColor(1,1,1,1), avScreenSize);
		mBaseObj.SetOnGuiFunction(asGuiFunction);
		mBaseObj.SetGuiVariableFPS(5);
		mBaseObj.SetGuiActive(true);
		mBaseObj.SetGuiUpdateWhenOutOfView(false);
	}
	
	//------------------------------------------------------------
		
	/////////////////////////////////////////
	// DEBUG
	/////////////////////////////////////////
	
	//------------------------------------------------------------
		
	float DrawDebugOutput(cGuiSet @apSet,iFontData @apFont,float afStartY)
	{
		return afStartY;
	}
	
	void OnRenderSolid(cRendererCallbackFunctions@ apFunctions)
	{
		/*if (mbIsMeat && Entity_IsInteractedWith(mBaseObj.GetName())==false)
		{
			apFunctions.GetLowLevelGfx().DrawSphere(mBaseObj.GetPosition(), mfMeatAttractDistance, cColor(1,0,0,1));	
		}*/
	}
	
	//------------------------------------------------------------
	
	/////////////////////////////////////////
	// PROPERTIES
	/////////////////////////////////////////
	
	//------------------------------------------------------------
	
	[nosave] tString msType;
	[nosave] eCrossHairState mCursor = eCrossHairState_PickUp;
	
	// Meat
	[volatile] iScrMeatComponent_Interface@ mpMeat = null;
	bool mbIsMeat = false;
	
	int mlItemContentLevel;
	int mlItemCustomContentLevel;
	int mlLuckyRoll = 30;
	bool mbOnlyNeedOne = false;
	bool mbPutAwayAfter = true;
	bool mbUseFullnessAnimation = false;
	bool mbHighlight = true;
	tString msAnimationName; //Saved because it is a value copied between entities
	tString msPickupSound;
	
	//------------------------------------------------------------
}	